plugins {
  id "c"
  id "java"
  id "com.github.johnrengelman.shadow" version "5.2.0"
}

repositories {
  mavenLocal()
  jcenter()
}

dependencies {
//  implementation("net.java.dev.jna:jna:5.5.0")
//  implementation("net.java.dev.jna:jna-platform:5.5.0")
  implementation("net.bytebuddy:byte-buddy-agent:1.10.11")
  implementation("net.metzweb:tinyserver:1.0.0")
}

model {
  platforms {
    x64 {
      architecture "x64"
    }
  }

  components {
    jvmtiInstanceCounter(NativeLibrarySpec) {
      targetPlatform "x64"
      sources {
        c {
          source {
            srcDir "src/main/c"
            include "**/*.c"
          }
          exportedHeaders {
            srcDir "src/main/headers"
          }
        }
      }
      binaries.all {
        if (targetPlatform.operatingSystem.linux) {
          cCompiler.args '-I', "${org.gradle.internal.jvm.Jvm.current().javaHome}/include"
          cCompiler.args '-I', "${org.gradle.internal.jvm.Jvm.current().javaHome}/include/linux"
          cCompiler.args '-D_FILE_OFFSET_BITS=64'
        } else if (targetPlatform.operatingSystem.windows) {
          cCompiler.args "-I${org.gradle.internal.jvm.Jvm.current().javaHome}/include"
          cCompiler.args "-I${org.gradle.internal.jvm.Jvm.current().javaHome}/include/win32"
//          linker.args "Shlwapi.lib", "Advapi32.lib"
        }
      }
    }
  }
}

def mainClassName = "com.mageddo.jvmti.HelloWorld"

task run(type: JavaExec) {
  main = mainClassName
  classpath sourceSets.main.runtimeClasspath
  classpath configurations.runtime
}


jar {
//  dependsOn(assembleVariant)
  manifest {
    attributes (
      "Main-Class": mainClassName,
      "Premain-Class": "com.mageddo.jvmti.agents.ClassAgent",
      "Agent-Class": "com.mageddo.jvmti.agents.ClassAgent"
    )
  }
  from("${buildDir}/libs/jvmtiInstanceCounter/shared") {
    into "META-INF/resources/"
  }
}

shadowJar {
  mergeServiceFiles()
  transform(com.github.jengelman.gradle.plugins.shadow.transformers.PropertiesFileTransformer) {
    mergeStrategy = "append"
  }
}